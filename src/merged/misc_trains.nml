/* *** Begin Header *** */

grf {
	grfid: "TT18";
	name: string(STR_GRF_NAME);
	desc: string(STR_GRF_DESC);
	version: 0;
	min_compatible_version: 0;

	url: string(STR_WEBSITE);
	
	param 1 {
		param_company_colours {
			name: string(STR_PARAM_NAME_CCOLOUR);
			desc: string(STR_PARAM_DESC_CCOLOUR);
			min_value: 0;
			max_value: 1;
			def_value: 0;
			names: {
				0: string(PARAM_CCOLOUR_REALISTIC);
				1: string(PARAM_CCOLOUR_CC);
			};
		}
	}
}

railtypetable {
	RAIL, ELRL
}

cargotable {
	PASS
}

/* Disable all base game trains. */
disable_item(FEAT_TRAINS);

/*
Base costs are tweaked to balance vehicle purchase and running costs using a strategy developed by Andythenorth in his excellent Iron Horse 2 train set. 
This requires changing base costs to give greater range and control, and allow me to borrow values directly from the Iron Horse code reference. Thanks, Andy! :)

Note that all engines use RUNNING_COST_STEAM and all wagons use RUNNING_COST_DIESEL. 
Unlike other base cost overrides, changing vehicle base costs only affects vehicles within that NewGRF, so other vehicle sets will not be affected.
*/

basecost {
	PR_BUILD_VEHICLE_TRAIN: -1;
	PR_BUILD_VEHICLE_WAGON: 1;
	PR_RUNNING_TRAIN_STEAM: -3;
	PR_RUNNING_TRAIN_DIESEL: -4;
}

/* Sort vehicles in the purchase menu.

Vehicle IDs are set manually for each train so updates are savegame-compatible.

0: SJ Y2 "Kustpilen"

*/

/* *** End Header *** */


/* Begin Sprite Templates */

/* Fix depot view */
traininfo_y_offset = 2;
train_width_32_px = 1;

/* Recolour switches for realistic or company colours */

/* DSB */
switch (FEAT_TRAINS, SELF, sw_cc_red, param_company_colours) {
	0: return palette_2cc(COLOUR_RED, COLOUR_DARK_BLUE);
	1: return base_sprite_2cc + CB_RESULT_COLOUR_MAPPING_ADD_CC;
}

template template_8_standard(x, y) {
    //left_x, upper_y, width, height, offset_x, offset_y
	//							more negative to shift left
	//											more negative to shift up
    [ x,		y,		8,		28,		-3,			-14]
    [ x+16,		y,		22,		23,		-15,		-13]
    [ x+46,		y,		32,		14,		-16,		-10]
    [ x+86,		y,		22,		23,		-5,			-13]
    [ x+116,	y,		8,		28,		-3,			-14]
    [ x+132,	y,		22,		23,		-15,		-13]
    [ x+162,	y,		32,		14,		-16,		-10]
    [ x+202,	y,		22,		23,		-5,			-13]
}

template template_8_reversed(x, y) {
    //left_x, upper_y, width, height, offset_x, offset_y
	//							more negative to shift left
	//											more negative to shift up
    [ x+116,	y,		8,		28,		-3,			-14]
    [ x+132,	y,		22,		23,		-15,		-13]
    [ x+162,	y,		32,		14,		-16,		-10]
    [ x+202,	y,		22,		23,		-5,			-13]
	[ x,		y,		8,		28,		-3,			-14]
    [ x+16,		y,		22,		23,		-15,		-13]
    [ x+46,		y,		32,		14,		-16,		-10]
    [ x+86,		y,		22,		23,		-5,			-13]
}

/* End Sprite Templates */


/* Begin Functions */

switch (FEAT_TRAINS, SELF, func_running_costs, cost, current_speed) {
	0: return cost / 10; // Stopped
	1..15: return cost / 4; // Moving below ~33.5 mph
	return cost; // Track speed
}

switch (FEAT_TRAINS, PARENT, func_is_reversed, vehicle_is_reversed) {return;}

/* End Functions */


/* Begin MF */

/*
*	Note: Class MF consist is MFA-FF-MFB.
*/

/* GRAPHICS */

/* Lights */
spriteset(spriteset_mf_headlights, "src\gfx\mf_headlights.png") {template_8_standard(0,0)}
spriteset(spriteset_mf_marker_lights, "src\gfx\mf_marker_lights.png") {template_8_standard(0,0)}

spriteset(spriteset_mf_1_mfa, "src\gfx\mf_1_mfa_8bpp.png") {template_8_standard(0,0)}
spriteset(spriteset_mf_1_mfa_reversed, "src\gfx\mf_1_mfa_8bpp.png") {template_8_reversed(0,0)}
spriteset(spriteset_mf_1_ff, "src\gfx\mf_1_ff_8bpp.png") {template_8_standard(0,0)}
spriteset(spriteset_mf_1_ff_reversed, "src\gfx\mf_1_ff_8bpp.png") {template_8_reversed(0,0)}
spriteset(spriteset_mf_1_mfb, "src\gfx\mf_1_mfb_8bpp.png") {template_8_standard(0,0)}
spriteset(spriteset_mf_1_mfb_reversed, "src\gfx\mf_1_mfb_8bpp.png") {template_8_reversed(0,0)}

/* SWITCHES */

/* First car of vehicle is an MFA. If it is the first car of the train, display headlights.  */
switch (FEAT_TRAINS, SELF, sw_mf_mfa_with_headlights, [
    STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 1 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100),
    getbits(extra_callback_info1, 8, 8)
    ]) {
    0: return spriteset_mf_1_mfa;    // Return the base sprite
    1: return spriteset_mf_headlights;    // Return the overlay
}

/* First car of vehicle is an MFA. If it is the first car of the train, display headlights.  */
switch (FEAT_TRAINS, SELF, sw_mf_mfa_position, position_in_consist) {
	0: return sw_mf_mfa_with_headlights;
	return spriteset_mf_1_mfa;
}

/* Sprite stack for MFA with marker lights */
switch (FEAT_TRAINS, SELF, sw_mf_mfa_with_marker_lights, [
    STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 1 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100),
    getbits(extra_callback_info1, 8, 8)
    ]) {
    0: return spriteset_mf_1_mfa_reversed;    // Return the base sprite
    1: return spriteset_mf_marker_lights;    // Return the overlay
}

/* Last car of vehicle is an MFA. If it is the last car of the train, display marker lights.  */
switch (FEAT_TRAINS, SELF, sw_mf_mfa_reversed_position, position_in_consist_from_end) {
	0: return sw_mf_mfa_with_marker_lights;
	return spriteset_mf_1_mfa_reversed;
}

/* Sprite stack for MFB with headlights */
switch (FEAT_TRAINS, SELF, sw_mf_mfb_with_headlights, [
    STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 1 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100),
    getbits(extra_callback_info1, 8, 8)
    ]) {
    0: return spriteset_mf_1_mfb_reversed;    // Return the base sprite
    1: return spriteset_mf_headlights;    // Return the overlay
}

/* First car of vehicle is an MFB. If it is the first car of the train, display headlights.  */
switch (FEAT_TRAINS, SELF, sw_mf_mfb_reversed_position, position_in_consist) {
	0: return sw_mf_mfb_with_headlights;
	return spriteset_mf_1_mfb_reversed;
}

/* Sprite stack for MFB with marker lights */
switch (FEAT_TRAINS, SELF, sw_mf_mfb_with_marker_lights, [
    STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 1 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100),
    getbits(extra_callback_info1, 8, 8)
    ]) {
    0: return spriteset_mf_1_mfb;    // Return the base sprite
    1: return spriteset_mf_marker_lights;    // Return the overlay
}

/* Last car of vehicle is an MFB. If it is the last car of the train, display marker lights.  */
switch (FEAT_TRAINS, SELF, sw_mf_mfb_position, position_in_consist_from_end) {
	0: return sw_mf_mfb_with_marker_lights;
	return spriteset_mf_1_mfb;
}

/* Car 0 check reversed */
switch (FEAT_TRAINS, PARENT, sw_mf_reverser_0, vehicle_is_reversed) {
	0: sw_mf_mfa_position;
	sw_mf_mfb_reversed_position;
}

/* Car 1 check reversed */
switch (FEAT_TRAINS, PARENT, sw_mf_reverser_1, vehicle_is_reversed) {
	0: spriteset_mf_1_ff; // This car has no headlights/marker lights, so we can go directly to the spriteset.
	spriteset_mf_1_ff_reversed; // This car is symmetrical, but un-reverse the sprite to hide any rendering inconsistencies.
}

/* Car 2 check reversed */
switch (FEAT_TRAINS, PARENT, sw_mf_reverser_2, vehicle_is_reversed) {
	0: sw_mf_mfb_position;
	sw_mf_mfa_reversed_position;
}

/* Select the correct part of the train */
switch (FEAT_TRAINS, SELF, sw_mf_graphics, position_in_articulated_veh) {
	0: sw_mf_reverser_0; // Car 0 (first)
	1: sw_mf_reverser_1; // Car 1 (middle)
	2: sw_mf_reverser_2; // Car 2 (last)
}

/* Assemble a 3-car articulated vehicle. */
switch (FEAT_TRAINS, SELF, sw_mf_articulated_part, extra_callback_info1) {
	1 .. 2: return item_mf;
	return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

/* ITEM*/

item(FEAT_TRAINS, item_mf, 1) {
	property {
		/* Common properties */
		name: string(STR_NAME_MF);
		climates_available: ALL_CLIMATES;
		introduction_date: date(1989,01,01);
		model_life:	40;
		vehicle_life: 40;
		reliability_decay: 20;
		refittable_cargo_classes: bitmask(CC_PASSENGERS);
		default_cargo_type: PASS;
		cargo_capacity: 50;
		loading_speed: 6;
		/* Train-specific properties */
		sprite_id: SPRITE_ID_NEW_TRAIN; //enable new graphics
		speed: 180 km/h;
		misc_flags: bitmask(TRAIN_FLAG_2CC, TRAIN_FLAG_MU, TRAIN_FLAG_SPRITE_STACK);
		refit_cost: 0;
		track_type:	RAIL;
		ai_special_flag: AI_FLAG_PASSENGER;
		running_cost_base: RUNNING_COST_STEAM;
		engine_class: ENGINE_CLASS_DIESEL;
		power: 1600 hp;
		weight: 97 ton;
		tractive_effort_coefficient: 0.3;
		length: 8;
	}
	graphics {
		default: sw_mf_graphics;
		purchase: spriteset_mf_1_mfa;
		colour_mapping: sw_cc_red;
		articulated_part: sw_mf_articulated_part;
		cargo_capacity: return (position_in_articulated_veh == 1) ? 64 : 37;
		purchase_cargo_capacity: 140 / 3;
		cost_factor: 90;
		running_cost_factor: func_running_costs(1800);
		purchase_running_cost_factor: 1800;
		visual_effect_and_powered: (position_in_articulated_veh == 1) ? visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER) : visual_effect_and_powered(VISUAL_EFFECT_DIESEL, 0, ENABLE_WAGON_POWER);
		additional_text: string(STR_ROLE_PASS);
	}
}

/* End MF */
